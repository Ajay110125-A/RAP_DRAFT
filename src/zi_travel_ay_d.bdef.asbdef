managed;
strict ( 2 );

define behavior for ZI_TRAVEL_AY_D alias _Travel
implementation in class ZCL_BP_TRAVEL_AY_D unique
persistent table zaj_travel_d
lock master
authorization master ( instance, global )
//etag master <field_name>
{
  create ( precheck );
  update ( precheck );
  delete;
  field ( numbering : managed, readonly ) TravelUUID;
  field ( readonly ) TravelId, OverallStatus;
  association _Booking { create ( authorization : update ); }

  action ( authorization : update ) acceptTravel result [1] $self;
  action ( authorization : update ) rejectTravel result [1] $self;
  action ( authorization : update ) discount parameter ZAJ_DEDUCT_DISCOUNT result [1] $self;

  internal action reCalTotalPrice;

  mapping for zaj_travel_d
    {
      AgencyId           = agency_id;
      BeginDate          = begin_date;
      BookingFee         = booking_fee;
      CurrencyCode       = currency_code;
      CustomerId         = customer_id;
      Description        = description;
      EndDate            = end_date;
      LastChangedAt      = last_changed_at;
      LocalCreatedAt     = local_created_at;
      LocalCreatedBy     = local_created_by;
      LocalLastChangedAt = local_last_changed_at;
      LocalLastChangedBy = local_last_changed_by;
      OverallStatus      = overall_status;
      TotalPrice         = total_price;
      TravelUUID         = travel_uuid;
      TravelId           = travel_id;
    }

  validation validateCustomerID on save { create; update; field CustomerId; }
  validation validateAgency on save { create; update; field AgencyId; }
  validation validateBeginDate on save { create; update; field BeginDate; }
  validation validateEndDate  on save { create; update; field EndDate; }

  determination setTravelId on save { create; }
  determination setStatus on modify { create; update; }
  determination calTotalPrice on modify { create; field BookingFee, CurrencyCode; }

  side effects { field BookingFee affects field TotalPrice; }

}

define behavior for ZI_BOOKING_AY_D alias _Booking
implementation in class ZCL_BP_BOOKING_AY_D unique
persistent table zaj_booking_d
lock dependent by _Travel
authorization dependent by _Travel
//etag master <field_name>
{
  update;
  delete;

  field ( numbering : managed, readonly ) BookingUUID;
  field ( readonly ) TravelUUID, BookingId, BookingDate;

  association _Travel;
  association _BookingSup { create ( authorization : update ); }

  mapping for zaj_booking_d
    {
      CarrierId          = carrier_id;
      BookingDate        = booking_date;
      BookingID          = booking_id;
      BookingStatus      = booking_status;
      BookingUUID        = booking_uuid;
      ConnectionID       = connection_id;
      CurrencyCode       = currency_code;
      CustomerID         = customer_id;
      FlightDate         = flight_date;
      FlightPrice        = flight_price;
      LocalLastChangedAt = local_last_changed_at;
      TravelUUID         = parent_uuid;
    }

  determination setBookingId   on save { create; }
  determination setBookingDate on save { create; }
  determination calTotalPrice  on modify { create; field FlightPrice, CurrencyCode; }

}

define behavior for ZI_BKSUPP_AY_D alias _BookingSup
implementation in class ZCL_BP_BKSUPP_AY_D unique
persistent table zaj_bksupp_d
lock dependent by _Travel
authorization dependent by _Travel
//etag master <field_name>
{
  update;
  delete;
  field ( numbering : managed, readonly ) BookSupplUUID;
  field ( readonly ) TravelUUID, BookingUUID;
  association _Travel;
  association _Booking;
  mapping for zaj_bksupp_d
    {
      Price               = price;
      BookSupplUUID       = booksuppl_uuid;
      BookingSupplementID = booking_supplement_id;
      BookingUUID         = parent_uuid;
      CurrencyCode        = currency_code;
      LocalLastChangedAt  = local_last_changed_at;
      SupplementID        = supplement_id;
      TravelUUID          = root_uuid;
    }


























}