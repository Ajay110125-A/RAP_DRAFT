managed;
strict ( 2 );

with draft;

define behavior for ZI_TRAVEL_AY_D alias _Travel
implementation in class ZCL_BP_TRAVEL_AY_D unique
persistent table zaj_travel_d
draft table zaj_travel_d_d
lock master
total etag LastChangedAt
authorization master ( instance, global )
etag master LocalLastChangedAt
{
  create ( precheck );
  update ( precheck );
  delete;
  field ( numbering : managed, readonly ) TravelUUID;
  field ( readonly ) TravelId, OverallStatus;
  association _Booking { create ( authorization : update ); with draft; }

  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;
  action ( features : instance ) discount parameter ZAJ_DEDUCT_DISCOUNT result [1] $self;

  internal action reCalTotalPrice;

  draft action Activate optimized;
  draft action Edit;
  draft action Resume;
  draft action Discard;
  draft determine action Prepare
  {
    validation validateCustomerID;
    validation validateAgency;
    validation validateBeginDate;
    validation validateEndDate;
    validation _Booking~validateCustomer;
    validation _Booking~validateCarrierId;
    validation _Booking~validateConnectionId;
    validation _Booking~validateFlightDate;
    validation _BookingSup~validateSupplimentId;
  }


  mapping for zaj_travel_d
    {
      AgencyId           = agency_id;
      BeginDate          = begin_date;
      BookingFee         = booking_fee;
      CurrencyCode       = currency_code;
      CustomerId         = customer_id;
      Description        = description;
      EndDate            = end_date;
      LastChangedAt      = last_changed_at;
      LocalCreatedAt     = local_created_at;
      LocalCreatedBy     = local_created_by;
      LocalLastChangedAt = local_last_changed_at;
      LocalLastChangedBy = local_last_changed_by;
      OverallStatus      = overall_status;
      TotalPrice         = total_price;
      TravelUUID         = travel_uuid;
      TravelId           = travel_id;
    }

  //Validations
  validation validateCustomerID on save { create; update; field CustomerId; }
  validation validateAgency on save { create; update; field AgencyId; }
  validation validateBeginDate on save { create; update; field BeginDate; }
  validation validateEndDate on save { create; update; field EndDate; }

  //Determinations
  determination setTravelId on save { create; }
  determination setStatus on modify { create; field OverallStatus; }
  determination calTotalPrice on modify { create; field BookingFee, CurrencyCode; }

  //Side Effects
  side effects { field BookingFee affects field TotalPrice; }

}

define behavior for ZI_BOOKING_AY_D alias _Booking
implementation in class ZCL_BP_BOOKING_AY_D unique
persistent table zaj_booking_d
draft table zaj_booking_d_d
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;

  field ( numbering : managed, readonly ) BookingUUID;
  field ( readonly ) TravelUUID, BookingId, BookingDate, BookingStatus;

  action ( authorization : update ) Accept result [1] $self;
  action ( authorization : update ) Cancel result [1] $self;

  association _Travel { with draft; }
  association _BookingSup { create ( authorization : update ); with draft; }

  mapping for zaj_booking_d
    {
      CarrierId          = carrier_id;
      BookingDate        = booking_date;
      BookingID          = booking_id;
      BookingStatus      = booking_status;
      BookingUUID        = booking_uuid;
      ConnectionID       = connection_id;
      CurrencyCode       = currency_code;
      CustomerID         = customer_id;
      FlightDate         = flight_date;
      FlightPrice        = flight_price;
      LocalLastChangedAt = local_last_changed_at;
      TravelUUID         = parent_uuid;
    }

  //Validations
  validation validateCustomer on save { create; field CustomerId; }
  validation validateCarrierId on save { create; field CarrierId; }
  validation validateConnectionId on save { create; field ConnectionId; }
  validation validateFlightDate on save { create; field FlightDate; }

  //Determinations
  determination setBookingId on save { create; field BookingId; }
  determination setBookingDate on save { create; field BookingDate; }
  determination setBookingStatus on save { create; field BookingStatus; }
  determination calTotalPrice on modify { create; field FlightPrice, CurrencyCode; }

  //Side Effects
  side effects { field FlightPrice affects field _Travel.TotalPrice; }

}

define behavior for ZI_BKSUPP_AY_D alias _BookingSup
implementation in class ZCL_BP_BKSUPP_AY_D unique
persistent table zaj_bksupp_d
draft table zaj_bksupp_d_d
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( numbering : managed, readonly ) BookSupplUUID;
  field ( readonly ) TravelUUID, BookingUUID;
  field ( mandatory ) SupplementId;
  association _Travel { with draft; }
  association _Booking { with draft; }
  mapping for zaj_bksupp_d
    {
      Price              = price;
      BookSupplUUID      = booksuppl_uuid;
      //      BookingSupplementID = booking_supplement_id;
      BookingUUID        = parent_uuid;
      CurrencyCode       = currency_code;
      LocalLastChangedAt = local_last_changed_at;
      SupplementID       = supplement_id;
      TravelUUID         = root_uuid;
    }

  //Validations
  validation validateSupplimentId on save { create; field SupplementId; }

  //Determinations
  determination recalTotalPrice on modify { create; update; field Price; }

  //Side Effeets
  side effects { field Price affects field _Travel.TotalPrice; }






}